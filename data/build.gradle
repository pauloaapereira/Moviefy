import com.pp.moviefy.buildsrc.Configs
import com.pp.moviefy.buildsrc.Libs

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename)
        }
    } else {
        print(filename + " does not exist!")
    }
}

android {
    compileSdkVersion Configs.CompileSdkVersion

    defaultConfig {
        minSdkVersion Configs.MinSdkVersion
        targetSdkVersion Configs.TargetSdkVersion
        versionCode Configs.VersionCode
        versionName Configs.VersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "TMDB_V3_KEY", getProperty("secrets.properties", "TMDB_API_KEY"))
        buildConfigField("String", "TMDB_V4_TOKEN", getProperty("secrets.properties", "TMDB_ACCESS_TOKEN"))
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        // Multiple dependency bring these files in. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
}

dependencies {
    implementation project(':domain')

    implementation Libs.Hilt.library
    kapt Libs.Hilt.googleAndroidCompiler
    kapt Libs.Hilt.googleCompiler

}